plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.nuriggum'
version = '1.0.0'

java {
	sourceCompatibility = '11'
}

configurations {
	asciidoctorExt // asciidoctorExt에 대한 종속성 구성 선언
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
//	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.hibernate:hibernate-validator:7.0.4.Final'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.postgresql:postgresql:42.5.1'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3'

	implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4:1.16'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3' //ObjectMapper 사용시 필요
	implementation 'org.springframework.data:spring-data-commons' // Pageable 사용시 필요

	implementation 'org.assertj:assertj-core:3.22.0'

	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor' // asciidoctorExt에 의존성 추가
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' // MockMvc 의존성 추가
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

ext { // 스니펫 생성 디렉토리 설정
	snippetsDir = file('build/generated-snippets')
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-base:latest'
}

tasks.named('test') {
	outputs.dir snippetsDir // 스니펫 생성 test 테스크 설정
}

asciidoctor {
	doFirst {
		delete file('src/main/resources/static/docs')  //중복을 막기 위해 새로운 문서를 생성할 때에는 전에 생성했던 문서들을 먼저 지워준다.
	}
	dependsOn test // build시 test 후 asciidoctor를 진행하도록 설정해준다. (순서 설정)
	inputs.dir snippetsDir // 스니펫 위치 설정
	configurations 'asciidoctorExt' // asciidoctor 확장 설정
	baseDirFollowsSourceFile() //  .adoc 파일에서는 다른 .adoc 파일을 include하여 사용할 수 있는데 그럴 경우 경로를 동일하게 baseDir로 설정해준다. Gradle 6 버전에서는 자동으로 해주지만 7부터는 직접 명시해줘야 한다.
}

task copyDocument(type: Copy) { // (12)
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}